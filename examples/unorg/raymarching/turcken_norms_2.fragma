#version 330
out vec4 outputColor;
in vec2 UV;

uniform sampler2D bckbuffer;
uniform vec2 resolution;
uniform vec2 mouse;
uniform float time;

#define PI 3.1415

float sdSphere( vec3 p, float s ){
  return length(p)-s;
}

float sdBox( vec3 p, vec3 b ){
  vec3 q = abs(p) - b;
  return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
}

float sdCapsule( vec3 p, vec3 a, vec3 b, float r ){
  vec3 pa = p - a, ba = b - a;
  float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
  return length( pa - ba*h ) - r;
}

float sdVerticalCapsule( vec3 p, float h, float r ){
  p.y -= clamp( p.y, 0.0, h );
  return length( p ) - r;
}

float sdEllipsoid( vec3 p, vec3 r ){
  float k0 = length(p/r);
  float k1 = length(p/(r*r));
  return k0*(k0-1.0)/k1;
}

float sdTriPrism( vec3 p, vec2 h ){
  vec3 q = abs(p);
  return max(q.z-h.y,max(q.x*0.866025+p.y*0.5,-p.y)-h.x*0.5);
}

float sdRoundedCylinder( vec3 p, float ra, float rb, float h ){
  vec2 d = vec2( length(p.xz)-2.0*ra+rb, abs(p.y) - h );
  return min(max(d.x,d.y),0.0) + length(max(d,0.0)) - rb;
}

float sdRoundCone( vec3 p, float r1, float r2, float h )
{
  // sampling independent computations (only depend on shape)
  float b = (r1-r2)/h;
  float a = sqrt(1.0-b*b);

  // sampling dependant computations
  vec2 q = vec2( length(p.xz), p.y );
  float k = dot(q,vec2(-b,a));
  if( k<0.0 ) return length(q) - r1;
  if( k>a*h ) return length(q-vec2(0.0,h)) - r2;
  return dot(q, vec2(a,b) ) - r1;
}

float smin( float a, float b, float k ){
    float h = max( k-abs(a-b), 0.0 )/k;
    return min( a, b ) - h*h*k*(1.0/4.0);
}

vec2 rotate(vec2 p, float a){
    return mat2(cos(a), -sin(a), sin(a), cos(a)) * p;
}

float get_dist(vec3 p){
    
    float plane = p.y + 7.;
    p.y -= 1.5;
    
    p.xz = rotate(p.xz, time);
    //p.yz = rotate(p.yz, time);
    //p.yz = rotate(p.yz, .9);
    
    vec3 body_p = p;
    body_p = body_p - vec3(0.,-1.2,.5);
    body_p.yz = rotate(body_p.yz, .5);
    float body = sdEllipsoid(body_p, vec3(1.2,1.,1.3));
    
    vec3 tail_p = p;
    tail_p = tail_p - vec3(0.,-1.5,1.5);
    float tail_y = tail_p.y;
    tail_p.yz = rotate(tail_p.yz, tail_p.y*.05 + .5);
    float tail = sdRoundCone(tail_p, .5, .1, 3.1);
    
    vec3 arms_p = p;
    arms_p.x = abs(arms_p.x);
    arms_p -= vec3(.6,-.9,-.2);
    
    arms_p.xz = rotate(arms_p.xz, -.1);
    arms_p.xy = rotate(arms_p.xy, -.8);
    arms_p -= vec3(.9,.0,.0);
    
    float arms = sdEllipsoid(arms_p, vec3(.8,.2,.4));
    
    vec3 legs_p = p;
    legs_p.x = abs(legs_p.x);
    legs_p -= vec3(1.2,-2.4,.7);
    legs_p.xy = rotate(legs_p.xy, .5);
    legs_p.yz = rotate(legs_p.yz, .8);
    legs_p -= vec3(.0,.5,.0);
    float legs = 1e8;
    
    float thighs = sdEllipsoid(legs_p, vec3(.4,1.,.6));
    
    legs_p -= vec3(.0,-1.1,.0);
    legs_p.yz = rotate(legs_p.yz, -2.8);
    legs_p.xy = rotate(legs_p.xy, -.6);
    legs_p -= vec3(.0,-.9,.0);
    float calves = sdEllipsoid(legs_p, vec3(.2,.6,.2));
    
    legs_p -= vec3(.0,-.35,.1);
    legs_p.yz = rotate(legs_p.yz,-1.1);
    legs_p.xy = rotate(legs_p.xy,.5);
    legs_p -= vec3(.0,.4,.0);
    float feet = sdRoundCone(legs_p, .2, .3, .9);
    
    vec3 toe_p1 = legs_p;
    toe_p1 -= vec3(.0,.9,.0);
    toe_p1.yz = rotate(toe_p1.yz,1.2);
    toe_p1.xy = rotate(toe_p1.xy,.5);
    toe_p1 -= vec3(.0,.4,.0);
    float toes1 = sdVerticalCapsule(toe_p1, 1., .2);
    
    vec3 toe_p2 = legs_p;
    toe_p2 -= vec3(.0,.9,.0);
    toe_p2.yz = rotate(toe_p2.yz,1.2);
    toe_p2.xy = rotate(toe_p2.xy,.0);
    toe_p2 -= vec3(.0,.4,.0);
    float toes2 = sdVerticalCapsule(toe_p2, 1., .2);
    
    vec3 toe_p3 = legs_p;
    toe_p3 -= vec3(.0,.9,.0);
    toe_p3.yz = rotate(toe_p3.yz,1.2);
    toe_p3.xy = rotate(toe_p3.xy,-.5);
    toe_p3 -= vec3(.0,.4,.0);
    float toes3 = sdVerticalCapsule(toe_p3, 1., .2);
    
    float toes = smin(toes1, toes2, .25);
    toes = smin(toes, toes3, .25);
    
    feet = smin(feet, toes, .3);
    
    //legs = min(legs,thighs);
    //legs = min(legs,calves);
    
    vec3 hp = p - vec3(0.,.0,-2.);
    vec3 head_p = hp;
    float head = 1e8;
    float head_upper_tip = sdRoundedCylinder(head_p - vec3(0.,0.,-.5), .41, .3, .3);
    float head_upper = sdBox(head_p, vec3(.45,.2,.48)) - .4;
    
    head = min(head,head_upper_tip);
    head = smin(head,head_upper,.1);
    
    head_p.y += .5;
    head_p.yz = rotate(head_p.yz, -.25);
    float head_lower_tip = sdRoundedCylinder(head_p - vec3(0.,0.,-.5), .3, .1, .0);
    float head_lower = sdBox(head_p, vec3(.47,.0,.6)) - .1; 
    
    head = smin(min(head_lower_tip, head_lower), head, .1);
    //head = min(head,head_lower_tip);
    //head = min(head,head_lower);
    
    float eyes = 1e8;
    vec3 eyes_p = hp;
    eyes_p.x = abs(eyes_p.x);
    eyes_p = eyes_p - vec3(.9,.2,-.3);
    eyes_p.xy = rotate(eyes_p.xy, .2);
    eyes_p.yz = rotate(eyes_p.yz, -.1);
    float eyeball = sdEllipsoid(eyes_p, vec3(.17,.38,.36));
    
    float eyelids = eyeball-.05;
    eyelids = max(-eyeball,eyelids);
    eyelids = max(-eyes_p.y+.05,eyelids);
    
    eyes = min(eyes, eyeball);
    eyes = min(eyes, eyelids);
    
    float nose = 1e8;
    vec3 nose_p = hp;
    nose_p.x = abs(nose_p.x);
    nose_p = nose_p - vec3(.1,.3,-1.3);
    nose_p.xz = rotate(nose_p.xz, .5);
    nose_p.yz = rotate(nose_p.yz, -1.);
    
    float nostril = sdVerticalCapsule(nose_p,.3,.05);
    
    float neck = 1e8;
    vec3 neck_p = hp - vec3(0.,.2,.9);
    neck_p.yz = rotate(neck_p.yz, PI*.5+.1);
    neck_p.yz = rotate(neck_p.yz, neck_p.y*.5);
    
    neck = sdVerticalCapsule(neck_p,1.1,.35);
    
    vec3 crest_p = hp - vec3(.0,.9,.6);
    crest_p.yz = rotate(crest_p.yz, .5);
    float crest1 = sdEllipsoid(crest_p, vec3(.1,.3,.2));
    crest_p -= vec3(.0,.0,.35);
    crest_p.yz = rotate(crest_p.yz, .5);
    float crest2 = sdEllipsoid(crest_p, vec3(.1,.2,.15));
    
    float crest = min(crest1, crest2);
    
    head = head-.1;
    head = max(-nostril,head);
    head = smin(head,neck,.2);
    head = min(head,eyes);
    head = smin(head,crest,.15);
   
    //head = head-.1;
    
    float turcken = head;
    turcken = smin(turcken, body, .4);
    turcken = smin(turcken, tail, .3);
    turcken = smin(turcken, arms, .1);
    turcken = smin(turcken, thighs, .3);
    turcken = smin(turcken, calves, .2);
    turcken = smin(turcken, feet, .3);
    
    float dist = plane;
    dist = min(dist, turcken);
   
    return dist;
}

vec3 estimateNormal(vec3 p, float EPS){
	float xPl=get_dist(vec3(p.x+EPS,p.y,p.z));
	float xMi=get_dist(vec3(p.x-EPS,p.y,p.z));
	float yPl=get_dist(vec3(p.x,p.y+EPS,p.z));
	float yMi=get_dist(vec3(p.x,p.y-EPS,p.z));
	float zPl=get_dist(vec3(p.x,p.y,p.z+EPS));
	float zMi=get_dist(vec3(p.x,p.y,p.z-EPS));
	float xDiff=xPl-xMi;
	float yDiff=yPl-yMi;
	float zDiff=zPl-zMi;
	return normalize(vec3(xDiff,yDiff,zDiff));
}

void main() {
	vec2 uv = UV;
    vec2 rs = resolution;
    uv = (uv-.5)*rs/rs.y;
    
    vec3 camera = vec3(0.,1.,-10.5);
    vec3 lookat = vec3(0.,0.,0.);
    
    vec3 cam_dir = normalize(lookat - camera);
    
    float cam_screen_dst = 1.;
    vec3 screen_center = camera + cam_dir * cam_screen_dst;
    
    vec3 screen_y_axis = cross(cam_dir,cross(cam_dir,vec3(0.,-1.,0.))); 
    vec3 screen_x_axis = cross(screen_y_axis, cam_dir);
    
    screen_y_axis = normalize(screen_y_axis);
    screen_x_axis = normalize(screen_x_axis);
    
    vec3 screen_coords = screen_center + screen_y_axis * uv.y + screen_x_axis * uv.x;
    
    vec3 ray_origin = screen_coords;
    vec3 ray_dir = normalize(ray_origin-camera);
    
    float depth = 0.;
    for(int i=0; i<64; i++){
       float step_depth = get_dist(ray_origin);
       ray_origin += ray_dir * step_depth;
       depth += step_depth;
       if(depth < .01 || depth > 100){
        break;
       }
    }
    
    vec3 normal = estimateNormal(ray_origin, .001);
    
    vec3 col = normal;
    
	outputColor = vec4(col,1.);
}
/**========END_OF_FRAGMENT========**/